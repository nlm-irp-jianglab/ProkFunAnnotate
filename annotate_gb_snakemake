samples = []
dirs = []
for line in open(config['genomes']).readlines(): 
   l = line.strip().split("\t")
   samples.append(l[0])
   dirs.append(l[1])


rule all:
   input:
      expand("{out}/{sample}.gff", zip, sample=samples, out=dirs),
      expand("{out}/{sample}.emapper.annotations", zip, sample=samples, out=dirs),
      expand("{out}/{sample}.kofam.tsv", zip, sample=samples, out=dirs),
      
rule run_prokka: 
   singularity: "docker://keithdt/prokfunannotate:latest"
   input: 
      fasta="{out}/{sample}.fasta",
      genbank="{out}/{sample}.gb"
   output: 
      "{out}/{sample}.gff",
      "{out}/{sample}.faa"
   threads: 8
   resources:
       mem_gb = 24
   shell:
      """
      prokka --force --cpus {threads} --prefix {wildcards.sample} --outdir {wildcards.out}/{wildcards.sample}_prokka/ --proteins {input.genbank} {input.fasta}
      cp {wildcards.out}/{wildcards.sample}_prokka/{wildcards.sample}.gff {wildcards.out}/
      cp {wildcards.out}/{wildcards.sample}_prokka/{wildcards.sample}.faa {wildcards.out}/
      cp {wildcards.out}/{wildcards.sample}_prokka/{wildcards.sample}.tsv {wildcards.out}/{wildcards.sample}.prokka.tsv
      """


rule run_emapper:
   singularity: "docker://keithdt/prokfunannotate:latest"
   input: "{out}/{sample}.faa"
   output: "{out}/{sample}.emapper.annotations"
   threads: 8
   resources:
      mem_gb = 24
   shell:
      "emapper.py -i {input} -o {wildcards.out}/{wildcards.sample} --temp_dir /tmp  --data_dir /opt/egg_data/ --cpu {threads}"

rule run_kofamscan:
   singularity: "docker://keithdt/prokfunannotate:latest"
   input: "{out}/{sample}.faa"
   output: "{out}/{sample}.kofam.tsv"
   threads: 8
   resources:
      mem_gb = 24
   shell:
      "exec_annotation --tmp-dir /tmp -f detail-tsv -p /opt/kofam_data/profiles/ --ko-list /opt/kofam_data/ko_list --cpu {threads} -o {output} {input}"

